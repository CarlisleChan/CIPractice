apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
// Add checkstyle, findbugs, pmd and lint to the check task.
check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'lint'

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/StaticCodeCheck/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/StaticCodeCheck/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$project.buildDir/outputs/checkstyle/checkstyle.xml"
        }
        html {
            destination "$project.buildDir/outputs/checkstyle/checkstyle.html"
        }
    }
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("${project.rootDir}/StaticCodeCheck/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml {
            destination "$project.buildDir/outputs/findbugs/findbugs.xml"
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = true
    ruleSetFiles = files("${project.rootDir}/StaticCodeCheck/pmd/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$project.buildDir/outputs/pmd/pmd.xml"
        }
        html {
            destination "$project.buildDir/outputs/pmd/pmd.html"
        }
    }
}

android {
    lintOptions {
        abortOnError true
        xmlReport true
        htmlReport true
        lintConfig file("${project.rootDir}/StaticCodeCheck/lint/lint.xml")
        htmlOutput file("$project.buildDir/outputs/lint/lint-result.html")
        xmlOutput file("$project.buildDir/outputs/lint/lint-result.xml")
    }
}
